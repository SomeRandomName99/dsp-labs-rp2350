.program firmware_dsp

.pio_version 0

.side_set 2

; Pin assignments:
; - BCLK is side-set pin 0
; - WS is side-set pin 1
; - DATA is IN pin 0

.wrap_target
; Read in 16 bits and ignore the rest of the I2S Packet; Mic's resolution is 18
                          ;|\ WS
                          ;||\ BCLK
set x, 15           side 0b00
nop                 side 0b01 ; These 2 nops are needed because the ICS43434 microcphone
nop                 side 0b00 ; has it data ready on the second rising edge after WS instead of the first.
data_loop:
in pins, 1          side 0b01
jmp x--, data_loop  side 0b00 

; Stall cannot be use because BCLK must keep toggling
set x, 13           side 0b01 
time_loop:
nop                 side 0b00 
jmp x--, time_loop  side 0b01

set x, 30           side 0b10
bitloop2:
nop                 side 0b11 ; A nop is used because we only have one channel.
jmp x--, bitloop2   side 0b10
nop                 side 0b11
.wrap

% c-sdk {
static inline void firmware_dsp_program_init(PIO pio, uint sm, uint offset, uint pinBCLK, uint pinWS, uint pinData){
  pio_gpio_init(pio, pinBCLK);
  pio_gpio_init(pio, pinWS);
  pio_gpio_init(pio, pinData);

  pio_sm_set_consecutive_pindirs(pio, sm, pinBCLK, 1, true);
  pio_sm_set_consecutive_pindirs(pio, sm, pinWS, 1, true);
  pio_sm_set_consecutive_pindirs(pio, sm, pinData, 1, false);

  pio_sm_config c = firmware_dsp_program_get_default_config(offset);
  sm_config_set_sideset_pins(&c, pinBCLK);
  sm_config_set_in_pins(&c, pinData);
  // We autopush after 32, which will be after collecting 2 microphone samples(single channel)
  sm_config_set_in_shift(&c, false, true, 32);

  // To sample the mic at 48Khz with 150Mhz system clock we need a 3.072Mhz BLCK according to the datasheet.
  // Remember that each BLCK cycle is 2 clock cycles
  // 150Mhz/(2*3.072Mhz) = 24.414063 --> 24 integer and 106 fractional part
  sm_config_set_clkdiv_int_frac(&c, 24, 106);

  sm_config_set_fifo_join(&c, PIO_FIFO_JOIN_RX);

  pio_sm_init(pio, sm, offset, &c);
  pio_sm_set_enabled(pio, sm, true);
}
%}